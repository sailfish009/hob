(in-package :hob)

(defun as-var (word)
  (let ((name (h-word-name word)))
    ;; Obscure SBCL extension
    (when (equal name "/") (setf name "xxx/"))
    (if (equal name "_")
        (gensym)
        (intern name :hob.gen))))

(defun hcompile (expr)
  (match expr
    (:lit (h-lit-val expr))
    (:word (lookup-word expr :value :value))
    ((:seq exprs) (hcompile-seq exprs))
    (("#fn" args body)
     (let ((arg-syms (lmapseq (arg args)
                       (if (equal (h-word-name arg) "_")
                           (gensym)
                           (bind-word arg :value :value (as-var arg))))))
       `(lambda ,arg-syms ,(hcompile body))))
    (("#match" val cases)
     `(cond ,@(lmapseq (("->" lit body) cases)
                (if (and (h-word-p lit) (equal (h-word-name lit) "_"))
                    `(t ,(hcompile body))
                    `((equal ,(hcompile lit) ,(hcompile val)) ,(hcompile body))))))
    (("#fld" val i) `(svref ,(hcompile val) ,(h-lit-val i)))
    (("#assign" place val) `(setf ,(hcompile place) ,(hcompile val)))
    (("#assert" a b) `(unless (equal ,(hcompile a) ,(hcompile b)) (error "matching failed")))
    (("#assert" "()") '(error "matching failed"))
    ((fn . args) `(funcall ,(hcompile fn)
                           ,@(loop :for arg :in args :collect (hcompile arg))))))

(defun hcompile-seq (exprs)
  (let (vars top body)
    (dolist (expr exprs)
      (match expr
        (((:or "#def" "#var") name :_)
         (push (bind-word name :value :value (as-var name)) vars))
        (("#data" :_ variants)
         (loop :for variant :in (seq-list variants) :for i :from 1 :do
            (multiple-value-bind (name ctor)
                (match variant
                  (:word (values variant `(vector ,i)))
                  ((name . args)
                   (let ((syms (loop :repeat (length args) :collect (gensym))))
                     (values name `(lambda ,syms (vector ,i ,@syms))))))
              (push `(setf ,(as-var name) ,ctor) top)
              (push (bind-word name :value :value (as-var name)) vars))))
        (t)))
    (dolist (expr exprs)
      (match expr
        (((:or "#def" "#var") name val)
         (let ((set `(setf ,(as-var name) ,(hcompile val))))
           (if (match val (("#fn" . :_) t) (t nil)) (push set top) (push set body))))
        (("#data" . :_))
        (t (push (hcompile expr) body))))
    `(let (,@vars)
       ,@(nreverse top)
       ,@(nreverse body))))

(defun compile-s-expr (s-expr)
  (handler-bind ((warning #'muffle-warning))
    (cl:compile nil `(lambda ()
                       #+sbcl(declare (sb-ext:muffle-conditions sb-ext:compiler-note))
                       ,s-expr))))
